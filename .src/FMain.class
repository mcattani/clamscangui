' Gambas class file

'' ' *********************************************************************************************************
'' ' Ce programme est un logiciel libre; vous pouvez le redistribuer ou le modifier suivant
'' ' les termes de la GNU General Public License telle que publiée par la Free Software Foundation ;
'' ' soit la version 3 de la licence, soit( à votre gré) toute version ultérieure.
'' ' Ce programme est distribué dans l'espoir qu'il sera utile, mais SANS AUCUNE GARANTIE ;
'' ' sans même la garantie tacite de QUALITÉ MARCHANDE ou d'ADÉQUATION à UN BUT PARTICULIER.
'' ' Consultez la GNU General Public License pour plus de détails.
'' ' Vous devez avoir reçu une copie de la GNU General Public License en même temps que ce programme ;
'' ' si ce n 'est pas le cas, consultez <http://www.gnu.org/licenses>.
'' ' *********************************************************************************************************

Public SalidaERR As Boolean
Public SinINF As Boolean
Public Recursive As Boolean
Public SuprimirOK As Boolean
Public Infect As Boolean
Public Bell As Boolean
Public Remover As Boolean
Public AppsIndeseadas As Boolean
Public Mail As Boolean
Public PDF As Boolean
Public SWF As Boolean
Public HTML As Boolean
Public HWP3 As Boolean
Public PE As Boolean
Public ELF As Boolean
Public OLE2 As Boolean

Public maCle As Integer
Public Ruta As String                                                           'endroit ou fichier à scanner
Public sMot As String
Public bSortie As Boolean                                                       'drapeau de sortie de programme
Public bFlag As Boolean                                                         'drapeau boolean
Public quarantaine As String                                                    'nom du dossier de quarantaine
Public historic As String                                                       'nom du fichier historque
Public clamLog As String                                                        'nom du ficier log de session
Public Quarant As Boolean                                                       'drapeau pour quarantaine
Public antivir As String
Public langue As String                                                         'langue
Public bSudo As Boolean                                                         'sudo appliqué
Public bTBSudo As Boolean                                                       'si bouton sudo enfoncé
Public monTexte As String                                                       'texte de commande pour transfert
Public iFlag As Integer                                                         'drapeau integer

Public Sub Form_Open()                                                          'Form prinipale appelée par le Module1

     Dim leFichier As File
     Dim histo As File

     If Not Exist("/usr/bin/clamscan") Then
          Message.Title = ("¡Atención!")
          Message.Warning(("Debe tener instalado el antivirus ClamAv para poder utilizar esta App"))
          Me.Close
     Else
          txSalida.text = ("Versión instalada: ") & ClamScan.Version()
     Endif

     If Not Exist("/etc/clamav/clamd.conf") Then
          Message.Title = ("¡Atención!") & (" ~ Instalación incompleta de Clamav")
          Message(("Para reconfigurar clamd, ejecute sudo dpkg-reconfigure clamav-daemon") & "\n" & ("Para reconfigurar clamav-milter, ejecute sudo dpkg-reconfigure clamav-milter"))
          Me.Close
     Endif

     antivir = User.Home &/ "Clamav"
     quarantaine = User.Home &/ ".clamscangui/viruses"                     'dossier de quarantaine des virus
     clamLog = User.Home &/ ".clamscangui/ClamScanGui_Log.txt"             'fichier Log
     historic = User.Home &/ ".clamscangui/Historique.txt"                 'fichier historique
     If Not Exist(quarantaine) Then                                        'création du dossier de quarantaine des virus
          Try Mkdir User.Home &/ ".clamscangui"
          Try Mkdir quarantaine
     Endif

     If Not Exist(antivir) Then
          Try Mkdir antivir
     Endif

     If Not Exist(clamLog) Then                                            'fichier Log
          leFichier = Open clamLog For Create
     Endif

     If Not Exist(historic) Then                                           'fichier historique
          histo = Open historic For Create
     Endif

     quarantaine = Shell$(quarantaine)
     clamLog = Shell$(clamLog)
     historic = Shell$(historic)

     FMain.Text = Application.Name & " " & Application.Version

     If Settings["First_Time", True] = True Then                           'démarrage première fois
          Settings["First_Time"] = False
          premiereFois
     Endif
     LoadSettings()                                                        'réglages enregistrés
     Cerf.ShowModal()                                                      'splash form

End

Public Sub premiereFois()                                                       'réglages pour une première fois

     MnuAuto.Value = True
     MnuManuelle.Value = False
     MnuImmediate.Enabled = False
     MnuDeplacer.Value = False
     MnuVirus.Enabled = False

     Settings["Menu/Auto"] = MnuAuto.Value
     Settings["Menu/Manuel"] = MnuManuelle.Value
     Settings["Menu/Immediat"] = MnuImmediate.Enabled
     Settings["Menu/Deplacer"] = MnuDeplacer.Value
     Settings["Menu/Virus"] = MnuVirus.Enabled
     Settings["General/LangInit"] = Left(System.Language, 2)

     mnuHWP3.Value = True
     mnuXML.Value = True
     mnuSWF.Value = True
     mnuPdf.Value = True
     mnuMail.Value = True
     mnuPE.Value = True
     mnuELF.Value = True
     MnuOLE2.Value = True

     Settings["Menu/pHWP3"] = mnuHWP3.Value
     Settings["Menu/pXML"] = mnuXML.Value
     Settings["Menu/pSWF"] = mnuSWF.Value
     Settings["Menu/pPDF"] = mnuPdf.Value
     Settings["Menu/pMail"] = mnuMail.Value
     Settings["Menu/pPE"] = mnuPE.Value
     Settings["Menu/pELF"] = mnuELF.Value
     Settings["Menu/pOLE2"] = MnuOLE2.Value

End

Public Sub LoadSettings()                                                       'réglages enregistrés

     '************* Group Log1 *******************
     mnErrores.Value = Settings["Menu/Errores"]
     mnInfectados.Value = Settings["Menu/Infectes"]
     mnNoSummary.Value = Settings["Menu/NoSummary"]
     mnSupressOK.Value = Settings["Menu/SupprOK"]
     mnBell.Value = Settings["Menu/Bell"]
     '************* Group optionAvance **************
     mnRemover.Value = Settings["Menu/Remover"]
     mnDetectar.Value = Settings["Menu/Detectar"]
     MnuDeplacer.Value = Settings["Menu/Deplacer"]
     mnRecursivo.Value = Settings["Menu/Recursif"]
     '************************************************
     MnuAuto.Value = Settings["Menu/Auto"]
     MnuManuelle.Value = Settings["Menu/Manuel"]
     MnuImmediate.Enabled = Settings["Menu/Immediat"]
     MnuVirus.Enabled = Settings["Menu/Virus"]
     langue = Settings["General/LangInit"]
     '************* Group option1 ********************
     mnuHWP3.Value = Settings["Menu/pHWP3"]
     mnuXML.Value = Settings["Menu/pXML"]
     mnuSWF.Value = Settings["Menu/pSWF"]
     mnuPdf.Value = Settings["Menu/pPDF"]
     mnuMail.Value = Settings["Menu/pMail"]
     mnuPE.Value = Settings["Menu/pPE"]
     mnuELF.Value = Settings["Menu/pELF"]
     MnuOLE2.Value = Settings["Menu/pOLE2"]
     '********** Toutes options transmises **********************
     SalidaERR = mnErrores.Value
     infect = mnInfectados.Value
     SuprimirOK = mnSupressOK.Value
     SinINF = mnNoSummary.Value
     Bell = mnBell.Value

     mail = mnuMail.Value
     pdf = mnuPdf.Value
     Swf = mnuSWF.Value
     html = mnuXML.Value
     HWP3 = mnuHWP3.Value
     PE = mnuPE.Value
     ELF = mnuELF.Value
     OLE2 = MnuOLE2.Value

     Remover = mnRemover.Value
     Quarant = MnuDeplacer.Value
     Recursive = mnRecursivo.Value
     AppsIndeseadas = mnDetectar.Value
     '********************************************************

     If langue = "fr" Then
          MnuFrancais.Value = True
     Else If langue = "en" Then
          MnuAnglais.Value = True
     Else If langue = "es" Then
          MnuEspagnol.Value = True
          ' Else If langue = "ru" Then
          '      MenuRu.Value = True
          ' Else If langue = "pt" Then
          '      MenuPt.Value = True
          ' Else If langue = "de" Then
          '      MenuDe.Value = True
          ' Else If langue = "it" Then
          '      MenuIt.Value = True
     Endif

     If TBSudo.Value = True Then                                                'idem à TBSudo_Click
          bSudo = True
          bTBSudo = True
     Endif
     If TBSudo.Value = False Then
          bSudo = False
          bTBSudo = False
     Endif

End

Public Sub TBSudo_Click()                                                       'bouton sudo enfoncé

     If TBSudo.Value = True Then                                                'le bouton est enfoncé
          bSudo = True
          bTBSudo = True
          Message.Title = (("¡Atención!"))
          Message.Info(("¡Todos sus pedidos son \n del SuperUsuario!"))
     Endif
     If TBSudo.Value = False Then                                               'le bouton n'est pas enfoncé
          bSudo = False
          bTBSudo = False
     Endif

End

Public Sub SaveSettings()                                                       'sauvegarde des réglages

     '********** Group log1 *****************************
     Settings["Menu/Errores"] = mnErrores.Value
     Settings["Menu/Infectes"] = mnInfectados.Value
     Settings["Menu/NoSummary"] = mnNoSummary.Value
     Settings["Menu/SupprOK"] = mnSupressOK.Value
     Settings["Menu/Bell"] = mnBell.Value
     '******** Group optionAvance ***********************
     Settings["Menu/Recursif"] = mnRecursivo.Value
     Settings["Menu/Remover"] = mnRemover.Value
     Settings["Menu/Detectar"] = mnDetectar.Value
     Settings["Menu/Deplacer"] = MnuDeplacer.Value
     '******** recherche virus ********************
     Settings["Menu/Auto"] = MnuAuto.Value
     Settings["Menu/Manuel"] = MnuManuelle.Value
     Settings["Menu/Immediat"] = MnuImmediate.Enabled
     Settings["Menu/Virus"] = MnuVirus.Enabled

     Settings["General/LangInit"] = langue
     '********* Group option1 ****************************
     Settings["Menu/pHWP3"] = mnuHWP3.Value
     Settings["Menu/pXML"] = mnuXML.Value
     Settings["Menu/pSWF"] = mnuSWF.Value
     Settings["Menu/pPDF"] = mnuPdf.Value
     Settings["Menu/pMail"] = mnuMail.Value
     Settings["Menu/pPE"] = mnuPE.Value
     Settings["Menu/pELF"] = mnuELF.Value
     Settings["Menu/pOLE2"] = MnuOLE2.Value

End

Public Sub mnSalir_Click()

     Me.Close

End

Public Sub Form_KeyPress()

     If Key.code = Key.Esc Then Me.Close

End

Public Sub FC_Activate()

     menuFaux()
     Message.Info(("Por favor, espere unos segundos mientras inicia el antivirus"))
     Ruta = Shell$(FC.SelectedPath)
     ClamScan.ScanFile(ruta, SalidaERR, SinINF, SuprimirOK, SinINF, Bell, Remover, Mail, PDF, SWF, HTML, HWP3, PE, ELF, OLE2, Quarant, bSudo)
     menuVraix()                                                                'menus activés

End

Public Sub DC_Activate()

     menuFaux()
     Message.Info(("Por favor, espere unos segundos mientras inicia el antivirus"))
     Ruta = Shell$(DC.SelectedPath)
     ClamScan.ScanDir(ruta, SalidaERR, SinINF, SuprimirOK, SinINF, Bell, Remover, Recursive, AppsIndeseadas, Mail, PDF, SWF, HTML, HWP3, PE, ELF, OLE2, Quarant, bSudo)
     menuVraix()                                                                'menus désactivés

End

Public Sub _new()                                                               'pour remise à jour de certains éléments

     If Not IsNull(fc) Or Not IsNull(dc) Then
          maCle = 0
          Wait
          dc.Tooltip = ("Elija el directorio para escanear")
          fc.Tooltip = ("Elija el archivo para analizar")
     Endif
     menuVraix()

End

Public Sub FC_Change()                                                          'le file chooser a changé

     If fc.SelectedPath = "" Then
          _new()
     Else
          maCle = 1
          dc.Tooltip = ("")
          fc.Tooltip = ("")
     Endif

End

Public Sub DC_Change()                                                          'le dir chooser a changé

     If dc.SelectedPath = "" Then
          _new()
     Else
          maCle = 2
          dc.Tooltip = ("")
          fc.Tooltip = ("")
     Endif

End

Public Sub TBhProc_Click()                                                  'execute hproc

     If maCle = 0 Then
          Message.Info(("Seleccione \n an objeto para escanear!"))
     Else If maCle = 1 Then
          FC_Activate
     Else If maCle = 2 Then
          DC_Activate
     Endif

     _new()

End

Public Sub menuFaux()                                                           'menus et boutons inactivés

     mnOpciones.Enabled = False
     mnOpcionesAvanzadas.Enabled = False
     MnuBase.Enabled = False
     MnuAction.Enabled = False
     mnAbout.Enabled = False
     fc.Enabled = False
     dc.Enabled = False
     tp.Enabled = False
     TBhProc.Enabled = False
     TBeffacer.Enabled = False
     TBSudo.Enabled = False
     Wait

End

Public Sub menuVraix()                                                          'menus et boutons activés

     mnOpciones.Enabled = True
     mnOpcionesAvanzadas.Enabled = True
     MnuBase.Enabled = True
     MnuAction.Enabled = True
     mnAbout.Enabled = True
     fc.Enabled = True
     dc.Enabled = True
     tp.Enabled = True
     DC.Reload
     FC.Reload
     TBhProc.Enabled = True
     TBeffacer.Enabled = True
     TBSudo.Enabled = True
     Wait

End

Public Sub TBKill_Click()                                                       'Bouton kill hproc

     Wait
     If IsNull(ClamScan.hProc) Then
          _new()
          Message.Info(("Seleccione \n an objeto para escanear!"))
          Return
     Endif
     If ClamScan.hProc.State = ClamScan.hProc.Running Then
          If FMain.bSudo = True Then
               Authenticate.Command("sudo killall -u root -9 clamscan")              'montexte est affecté à la commande du process
               If FMain.bFlag = True Then
                    Shell FMain.monTexte
               Else
                    Shell "killall -u root -9 clamscan" & Application.Id
                    Message.Title = ("¡Atención!")
                    Message(("clamscan, todavía funciona, en segundo plano.\n Tendrá que borrar la memoria usted mismo."))
               Endif
          Else
               ClamScan.hProc.Kill                                                 'on tue le processus
          Endif

          Message(("El proceso actual ha sido eliminado") & ". " & ("¡Puedes volver!"))
          _new()
     Else
          Message(("¡El proceso ya se detuvo!"))
     Endif

End

Public Sub MnuHistoric_Click()                                                  'montre l'historique global

     FMain.Mouse = Mouse.Wait                                                   'au cas où le fichier serait gros
     Wait
     FrmHistoric.ShowModal()
     Me.Hide
     FMain.Mouse = Mouse.Default
     Me.Show

End

Public Sub MnuLog_Click()                                                       'montre les Logs

     FMain.Mouse = Mouse.Wait
     Wait
     Me.Hide
     FrmLog.ShowModal()
     FMain.Mouse = Mouse.Default
     Me.Show

End

Public Sub mnAcerca_Click()                                                     'menu à propos

     Me.Hide
     frmAbout.ShowModal()
     Me.Show

End

Public Sub mnFreshclam_Click()                                                  'montre le fichier freshclam Log

     FMain.Mouse = Mouse.Wait
     Wait
     Me.Hide
     frmFresh.ShowModal()
     FMain.Mouse = Mouse.Default
     Me.Show

End

Public Sub MnuVirus_Click()                                                     'Affichage du fichier des virus découverts

     FMain.Mouse = Mouse.Wait
     Wait
     Me.Hide
     Virus.ShowModal()
     FMain.Mouse = Mouse.Default
     Me.Show
     DC.Reload
     FC.Reload

End

Public Sub mnu_click()                                                          'group mnu

     If MnuManuelle.Checked = False Then
          sMot = "/etc/init.d/clamav-freshclam start"
          Authenticate.Command("sudo " & sMot)
          Wait
          If bFlag = True Then                                                  'bFlag=True si Auto validée dans Authenticate
               MnuAuto.Checked = True
               MnuManuelle.Checked = False
               MnuImmediate.Enabled = False
          Else
               MnuManuelle.Checked = True
               MnuImmediate.Enabled = True
               MnuAuto.Checked = False
               mnu_click
          Endif

     Else If MnuAuto.Checked = False Then
          sMot = "/etc/init.d/clamav-freshclam stop"
          Authenticate.Command("sudo " & sMot)
          Wait
          If bFlag = True Then
               MnuManuelle.Checked = True
               MnuImmediate.Enabled = True
               MnuAuto.Checked = False
          Else
               MnuAuto.Checked = True
               MnuManuelle.Checked = False
               MnuImmediate.Enabled = False
               mnu_click
          Endif

     Else If MnuAuto.Checked = True Then
          MnuManuelle.Checked = False
          MnuImmediate.Enabled = False

     Else If MnuManuelle.Checked = True Then
          MnuImmediate.Enabled = True
          MnuAuto.Checked = False

     Endif

End

Public Sub MnuImmediate_Click()                                                 'Mise à jour Freshclam manuelle et immédiate

     If MnuManuelle.Checked = True Then
          sMot = "sudo freshclam"
          Authenticate.Command(sMot)
          Wait
     Endif

End

Public Sub Form_Close()                                                         'fermeture de la Form principale

     If Exist(User.Home &/ ".clamscangui/ClamScanGui_Log.txt") Then             'effacement du fichier Log à la fermeture
          File.Save(User.Home &/ ".clamscangui/ClamScanGui_Log.txt", "")        'par enregistrement d'un fichier vide
     Endif

     SaveSettings()                                                             'sauvegarde des réglages avant sortie programme

     If Not IsNull(clamscan.hProc) Then
          If clamscan.hProc.State = clamscan.hProc.Running Then
               clamscan.hProc.Kill
               ' Shell "kill -9 " & clamscan.hProc.Handle                       'semble plus efficace que ligne au dessus
               Wait 1                                                           'attente d'une seconde pour kill process
               clamscan.hProc.Ignore = True                                     'fermeture de tout process en cours sans erreur
               Module1.cloture = True
               Module1.sortie()
          Endif
     Endif

     Me.Hide()

     Module1.quitAppli()                                                        'fermeture par le Module1

End

Public Sub MnuAide_Click()                                                      'affichage de l'aide

     Me.Hide
     FrmAide.ShowModal()
     Me.Show

End

Public Sub langue_Click()                                                       'choix de la langue

     If MnuEspagnol.Checked = True Then langue = "es"
     If MnuFrancais.Checked = True Then langue = "fr"
     If MnuAnglais.Checked = True Then langue = "en"
     AppliLangues()

End

Public Sub AppliLangues()                                                       'application du choix de langue

     Message.Title = ("El idioma elegido se aplicará después del reinicio")
     If Message.Question(("¿Quieres renunciar?") & gb.CrLf &
               ("El idioma elegido se aplicará después de reiniciar el programa."), ("Si"), ("No")) = 2 Then
          loadSettings()
     Else
          Settings["General/LangInit"] = langue
          Settings.Save
          ' Shell "gbx3 " & Application.Path                                    ' ATTENTION Cette ligne doit etre Décommenté lors du travail dans l'IDE  et etre commenter avant de générer l'executable
          Shell "kill -9 " & Application.id
          Exec [Application.Path & "/" & Application.Name]                      ' ATTENTION ligne à décommenter avant de générer l'executable
     Endif

End

Public Sub TBeffacer_Click()                                                    'bouton effacer le texte du textarea1

     FMain.txSalida.Text = ""

End

Public Sub Config_Click()                                                       'lecture ou édition des fichiers de config

     If MnuConfClamd.Value = True Then iFlag = 0
     If MnuConfFresh.Value = True Then iFlag = 1
     If MnuConfMilter.Value = True Then iFlag = 2

     FMain.Hide()
     FrmConf.ShowModal()
     FMain.Show()

End

Public Sub Exemples_Click()

     If MnuExempleClamd.Value = True Then iFlag = 3
     If MnuExempleFreshclam.Value = True Then iFlag = 4
     If MnuExempleMilter.Value = True Then iFlag = 5

     FMain.Hide()
     FrmConf.ShowModal()
     FMain.Show()

End

Public Sub Log1_Click()                                                         'menu des enregistrements

     Settings["Menu/Errores"] = mnErrores.Value
     Settings["Menu/Infectes"] = mnInfectados.Value
     Settings["Menu/NoSummary"] = mnNoSummary.Value
     Settings["Menu/SupprOK"] = mnSupressOK.Value
     Settings["Menu/Bell"] = mnBell.Value

     SalidaERR = mnErrores.Value                                                'valeurs transmises
     infect = mnInfectados.Value
     SuprimirOK = mnSupressOK.Value
     SinINF = mnNoSummary.Value
     Bell = mnBell.Value

     SaveSettings()

End

Public Sub option1_Click()                                                      'menu des options +

     Settings["Menu/pHWP3"] = mnuHWP3.Value
     Settings["Menu/pXML"] = mnuXML.Value
     Settings["Menu/pSWF"] = mnuSWF.Value
     Settings["Menu/pPDF"] = mnuPdf.Value
     Settings["Menu/pMail"] = mnuMail.Value
     Settings["Menu/pPE"] = mnuPE.Value
     Settings["Menu/pELF"] = mnuELF.Value
     Settings["Menu/pOLE2"] = MnuOLE2.Value

     mail = mnuMail.Value                                                       'valeurs transmises
     pdf = mnuPdf.Value
     Swf = mnuSWF.Value
     html = mnuXML.Value
     HWP3 = mnuHWP3.Value
     PE = mnuPE.Value
     ELF = mnuELF.Value
     OLE2 = MnuOLE2.Value

     SaveSettings()

End

Public Sub optionAvance_Click()                                                 'menu des options avancées

     If MnuDeplacer.Value = True Then MnuVirus.Enabled = True
     If MnuDeplacer.Value = False Then MnuVirus.Enabled = False

     Settings["Menu/Recursif"] = mnRecursivo.Value
     Settings["Menu/Remover"] = mnRemover.Value
     Settings["Menu/Detectar"] = mnDetectar.Value
     Settings["Menu/Deplacer"] = MnuDeplacer.Value

     Remover = mnRemover.Value                                                  'valeurs transmises
     Quarant = MnuDeplacer.Value
     Recursive = mnRecursivo.Value
     AppsIndeseadas = mnDetectar.Value

     SaveSettings()

End
